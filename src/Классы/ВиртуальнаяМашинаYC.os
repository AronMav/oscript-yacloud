#Использовать "../internal"
#Использовать json

Перем ИмяВиртуальнойМашины;

Процедура ПриСозданииОбъекта(СтрокаИмяВиртуальнойМашины)
	ИмяВиртуальнойМашины = СтрокаИмяВиртуальнойМашины;
КонецПроцедуры

// Возвращиет наименование виртуальной машины
//
// Возвращаемое значение:
//   Строка	- Имя виртуальной машины
//
Функция Имя() Экспорт
	Возврат ИмяВиртуальнойМашины;
КонецФункции

// Возвращиет параметры виртуальной машины
//
// Возвращаемое значение:
//   Структура - Параметры виртуальной машины
//
Функция ПараметрыНовой() Экспорт
	Структура = РаботаСКомандами.ПолучитьСтруктуруПараметров();
	Возврат Структура;
КонецФункции

// Выполняет создание виртуальной машины
//
// Параметры:
//	 ПараметрыМашины - Строка, Структура - Можно передать файл параметров
//	 или получить их использовав ВиртуальнаяМашинаYC.ПараметрыНовой()
//
// Возвращаемое значение:
//   Строка	- Свойства виртуальной машины
//
Функция Создать(Параметры) Экспорт
	Команда = РаботаСКомандами.СформироватьКомандуСозданияВМ(ИмяВиртуальнойМашины, Параметры);
	КонтекстВыполнения = ИсполнительКоманд.ВыполнитьКоманду(Команда);
	ПарсерJSON = Новый ПарсерJSON();
	СтруктураПараметров = ПарсерJSON.ПрочитатьJSON(СокрЛП(КонтекстВыполнения), , , Истина);
	Возврат СтруктураПараметров;
КонецФункции

// Выполняет запуск виртуальной машины
//
Процедура Запустить() Экспорт
	Команда = СтрШаблон("yc compute instance start %1 --no-user-output", ИмяВиртуальнойМашины);
	ИсполнительКоманд.ВыполнитьКоманду(Команда);
	Сообщить("ВМ запущена");
КонецПроцедуры

// Выполняет остановку виртуальной машины
//
Процедура Остановить() Экспорт
	Команда = СтрШаблон("yc compute instance stop %1 --no-user-output", ИмяВиртуальнойМашины);
	ИсполнительКоманд.ВыполнитьКоманду(Команда);
	Сообщить("ВМ остановлена");
КонецПроцедуры

// Выполняет перезапуск виртуальной машины
//
Процедура Перезапустить() Экспорт
	Команда = СтрШаблон("yc compute instance restart %1 --no-user-output", ИмяВиртуальнойМашины);
	ИсполнительКоманд.ВыполнитьКоманду(Команда);
	Сообщить("ВМ перезапущена");
КонецПроцедуры

// Возвращает свойства виртуальной машины
//
// Возвращаемое значение:
//   Структура	- Свойства виртуальной машины
//
Функция Свойства() Экспорт
	Команда = СтрШаблон("yc compute instance get %1 --no-user-output --format json", ИмяВиртуальнойМашины);
	КонтекстВыполнения = ИсполнительКоманд.ВыполнитьКоманду(Команда);
	ПарсерJSON = Новый ПарсерJSON();
	СтруктураПараметров = ПарсерJSON.ПрочитатьJSON(КонтекстВыполнения, , , Истина);
	Возврат СтруктураПараметров;
КонецФункции

// Возвращает внешний ip адрес виртуальной машины
//
// Возвращаемое значение:
//   Строка	- Внешний IP адрес
//
Функция IPАдресВнешний() Экспорт
	ПарсерJSON = Новый ПарсерJSON();
	СтруктураПараметров = Свойства();
	ВнешнийIPАдрес = СтруктураПараметров["network_interfaces"][0]["primary_v4_address"]["one_to_one_nat"]["address"];
	Возврат ВнешнийIPАдрес;
КонецФункции

// Выполняет подключение к виртуальной машине 
//
Процедура ВыполнитьКоманду(Пользователь, Каманда) Экспорт
	IPАдрес = IPАдресВнешний();
	Команда = СтрШаблон("ssh -o ""StrictHostKeyChecking no"" %1@%2 ""%3""", Пользователь, IPАдрес, Каманда);
	КонтекстВыполнения = ИсполнительКоманд.ВыполнитьКоманду(Команда);
	Сообщить(КонтекстВыполнения);
КонецПроцедуры

// Выполняет удаление виртуальной машины
//
Процедура Удалить() Экспорт
	Команда = СтрШаблон("yc compute instance delete --name %1 --no-user-output", ИмяВиртуальнойМашины);
	ИсполнительКоманд.ВыполнитьКоманду(Команда);
	Сообщить("ВМ удалена");
КонецПроцедуры